(defpackage parcom/toml
  (:use :cl)
  (:shadow #:string #:integer #:number #:boolean)
  (:import-from :parcom #:<*> #:<* #:*> #:<$)
  (:local-nicknames (#:p #:parcom)
                    (#:pd #:parcom/datetime))
  ;; --- Types --- ;;
  ;; --- Entry --- ;;
  ;; --- Parsers --- ;;
  (:export #:key))

(in-package :parcom/toml)

;; --- Types --- ;;

(defstruct tiered-key
  "A key that might point to a value several tables deep."
  (key nil :type list))

(defstruct table
  (key ""  :type tiered-key)
  (kvs nil :type hash-table))

;; --- Entry --- ;;

;; TODO: 2025-05-13 Don't forget about the "top-level table".
(defun toml (offset)
  "Parser: Parse a TOML document into a Hash Table."
  (funcall (p:alt #'comment #'pair) offset))

;; --- Parsers --- ;;

(defun comment (offset)
  "Parser: Skip over any comment line."
  (funcall (*> (p:char #\#)
               (p:consume (lambda (c) (not (equal c #\newline)))))
           offset))

#+nil
(comment (p:in "# yes"))

(defun string (offset)
  "Parser: One of the four TOML string types."
  (funcall (p:alt #'basic-string
                  #'multiline-basic-string
                  #'literal-string
                  #'multiline-literal-string)
           offset))

#+nil
(string (p:in "\"hel\\u00E9lo\""))

(defun basic-string (offset)
  "Parser: Parse the simplest kind of string."
  (p:fmap (lambda (chars) (concatenate 'cl:string chars))
          (funcall (p:between (p:char #\")
                              (p:many #'compound-char)
                              (p:char #\"))
                   offset)))

#+nil
(basic-string (p:in "\"hel\\u00E9lo\""))

(defun compound-char (offset)
  "Parser: Parse a char while being wary of escaping."
  (funcall (p:alt #'escaped-char (p:any-but #\")) offset))

(defun escaped-char (offset)
  (funcall (*> (p:peek (p:char #\\))
               (p:alt #'special-char #'p:control-char #'p:unicode))
           offset))

(defun special-char (offset)
  "Parser: Backslashes and quotes."
  (funcall (*> (p:char #\\)
               (p:alt (p:char #\\) (p:char #\")))
           offset))

(defun multiline-basic-string (offset)
  "Parser: Easily include newlines characters into strings and preserve them."
  (p:fmap (lambda (chars) (concatenate 'cl:string chars))
          (funcall (p:between (<* (p:string "\"\"\"") (p:opt #'p:newline))
                              (p:many (*> (p:opt (*> (p:char #\\)
                                                     #'p:multispace1))
                                          #'compound-char))
                              (p:string "\"\"\""))
                   offset)))

#+nil
(multiline-basic-string (p:in "\"\"\"hel\\u00E9lo\"\"\""))
#+nil
(multiline-basic-string (p:in "\"\"\"\\ a  \"\"\""))

(defun literal-string (offset)
  "Parser: Strings with no escaping. These parse much faster and are more
memory efficient than `basic-string'."
  (funcall (p:between (p:char #\')
                      (p:take-while (lambda (c) (not (equal c #\'))))
                      (p:char #\'))
           offset))

#+nil
(time (dotimes (n 10000)
        (literal-string (p:in "'yes indeed'"))))
#+nil
(time (dotimes (n 10000)
        (basic-string (p:in "\"yes indeed\""))))

(defun multiline-literal-string (offset)
  "Parser: Multiline strings with no escaping."
  (funcall (p:between (<* (p:string "'''") (p:opt #'p:newline))
                      (p:take-until (p:string "'''"))
                      (p:string "'''"))
           offset))

#+nil
(multiline-literal-string (p:in "'''the cat's catnip'''"))

(defun pair (offset)
  "Parser: A key-value pair."
  (funcall (<*> #'key
                (*> #'skip-space
                    (p:char #\=)
                    #'skip-space
                    #'value))
           offset))

(defun skip-space (offset)
  "A faster variant of `space' that just advances over whitespace chars."
  (funcall (p:consume (lambda (c) (or (equal c #\space) (equal c #\tab))))
           offset))

#+nil
(funcall #'skip-space (p:in "   abc"))

(defun key (offset)
  "Parser: A key that might be pointing several layers deep."
  (p:fmap (lambda (list) (make-tiered-key :key list))
          (funcall (p:sep (p:char #\.) (p:alt #'bare-key #'quoted-key))
                   offset)))

#+nil
(key (p:in "physical"))
#+nil
(key (p:in "physical.shape"))
#+nil
(key (p:in "site.\"google.com\""))

(defun bare-key (offset)
  "Parser: Just ASCII letters, digits, dashes, and underscores."
  (funcall (p:take-while1 (lambda (c)
                            (or (p:ascii-letter? c)
                                (p:digit? c)
                                (equal c #\-)
                                (equal c #\_))))
           offset))

#+nil
(bare-key (p:in "alex-honnold"))
#+nil
(bare-key (p:in "123"))

(defun quoted-key (offset)
  "Parser: Yuck don't do these."
  (funcall (p:alt #'basic-string #'literal-string) offset))

(defun table (offset)
  (p:fmap (lambda (x)
            (destructuring-bind (name kvs) x
              (let ((ht (make-hash-table :test #'equalp)))
                (dolist (pair kvs)
                  (setf (gethash (car pair) ht) (cadr pair)))
                (make-table :key name :kvs ht))))
          (funcall (<*> (<* (p:between (p:char #\[)
                                       #'key
                                       (p:char #\]))
                            #'p:multispace)
                        (p:sep-end #'p:multispace #'pair))
                   offset)))

#+nil
(table (p:in "[foo.bar]
bar = 1
baz = \"zoo\"
zoo = 1988-07-05
"))

(defun inline-table (offset)
  "Parser: The compact form of a table."
  (p:fmap (lambda (kvs)
            (let ((ht (make-hash-table :test #'equalp)))
              (dolist (kv kvs)
                (setf (gethash (car kv) ht) (cadr kv)))
              ht))
          (funcall (p:between (*> (p:char #\{) #'skip-space)
                              (p:sep (*> (p:char #\,) #'skip-space) #'pair)
                              (*> #'skip-space (p:char #\})))
                   offset)))

#+nil
(p:parse #'inline-table "{ first = \"Tom\", last = \"Preston-Werner\" }")

;; String
;; Integer
;; Float
;; Boolean
;; Offset-date time
;; Local date-time
;; Local date
;; Local time
;; Array
;; Inline table
(defun value (offset)
  "Parser: The value portion of a key-value pair."
  (funcall (p:alt #'string #'date-time #'number)
           offset))

(defun date-time (offset)
  (funcall (p:alt #'pd:offset-date-time #'pd:local-date-time #'pd:local-date #'pd:local-time)
           offset))

(defun number (offset)
  "Parser: Any number."
  (funcall (p:alt #'integer #'hex #'octal #'binary)
           offset))

(defun integer (offset)
  "Parser: Whole numbers."
  (p:fmap (lambda (ns)
            (destructuring-bind (head rest) ns
              (if (null rest)
                  head
                  (read-from-string (format nil "狺ㄣ镱桢徜蝈篝┅┅┅ㄦ躅汜祆吉鸷犰í鸷镳鸷汨狎＼┅＇鸷躅箝珙邃＇鸷轭翦珏颟鸷镳í鸷汨狎＼擤鸷箦鸷汨狎＼擤鸷翎脲麒殪灞＇鸷溟玳艨┅┅镦骟弭┅＋铋ㄩ铘彗弪鸷轭辈尝┅＋铋ㄩ铘彗弪鸷轭狈┅＋铋ㄩ铘彗弪鸷轭⒌尺垂卟脖┅＋铋ㄩ铘彗弪鸷轭⒈甙氨┅ㄤ彐躅桢镦骟弭⑿狎箦蚝痫箝糸鲥桢徜邈轫犰铛礅弪鸷骓狃灬礅溽铙蝈徜骝镯篝蜷铉ㄦ矧磲铋ｘ狺铙┅ㄦ躅汜祆í鸷篝蜷铉傍鸷箦鸨鸷汨狎＼擤鸷翎脲麒殪灞＇鸷桢咯┅镦骟弭┅＋铋ㄨ屮鸷轭傍溴徜哜邋姊┅ㄤ彐躅镢翎镦骟弭⑿狎箦蚝痫箝糸鲥忉箦铛礅弪鸷骓狃灬礅溽铙蝈徜骝镯篝蜷铉ㄦ矧磲铋ｏ狺铙┅ㄦ躅汜祆í鸷篝蜷铉帮鸷箦鸨鸷汨狎＼擤鸷翎脲麒殪灞＇鸷镢翎炜┅镦骟弭┅＋铋镢翎鸷轭帮氨渤吹斗┅＋铋镢翎鸷轭帮涪┅ㄤ彐躅忾钺蝙镦骟弭⑿狎箦蚝痫箝糸鲥忉箦铛礅弪鸷骓狃灬礅溽铙蝈徜骝镯篝蜷铉ㄦ矧磲铋ｂ狺铙┅ㄦ躅汜祆í鸷篝蜷铉扳鸷箦鸨鸷汨狎＼擤鸷翎脲麒殪灞＇鸷忾钺蝙咯┅镦骟弭┅＋铋ㄢ轭狎鸷轭扳卑卑┅ㄤ彐躅怙镬遽镦骟弭ㄦ躅汜祆鸷犰激鸷篝蜷铉Ⅳ蝓澧┅激铋鸷篝蜷铉㈡犰箦┅镦骟弭┅＋铋ㄢ镲戾犷鸷轭Ⅳ蝓澧┅